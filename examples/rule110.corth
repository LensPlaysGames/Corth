// Triangle side length
//          |
//          V
mem 9999 +  42  storeb

// Initialize column pattern
// The column pattern is simply a condition for each
//   column that is being drawn, in the form of a bit.
// It is updated throughout runtime, causing the fancy drawing
//   you see in the output to appear.
// Example pattern: 1100001010
// Example output:  **    * *

// Build initial table
//   Store a `1` at the first byte of memory
mem 1                    + 1 storeb
//   Store a `1` at (<triangle-side-length> - 1) in memory
mem mem 9999 + loadb 1 - + 1 storeb

0 // Loop over lines starting at `0`
while dup mem 9999 + loadb 2 - < do
  0 // Loop over columns starting at `0`, construct string at mem[10000]
  while dup mem 9999 + loadb < do
    // Test pattern to set current character to write to string.
    dup mem + loadb if
	  // Store a '*' at current column in string memory.
	  dup mem + 10000 + 42 storeb
	else
	  // Store a ' ' at current column in string memory.
	  dup mem + 10000 + 32 storeb
	endif

    // Increment column counter
    1 +
  endwhile
  dup mem + 10000 + 10 storeb // newline
  mem + 10000 + 1 + 0 storeb  // null terminator

  // Print constructed string
  mem 10000 + dump_s

  // Load Pattern
  mem 0 + loadb 1 << mem 1 + loadb or
  1 // Loop over columns, shift pattern
  while dup mem 9999 + loadb 1 - < do
    // Pattern to top of stack, ((pattern << 1) && 7)
  	swap 1 << 7 and
	// Copy index to top of stack, load value at memory address + index
	over mem + loadb or
	// Duplicate index + pattern
	twodup
	// ((110 >> pattern) && 1)
	110 swap >> 1 and
	// Swap duped index and pattern
	swap
	// Write new pattern to memory
	mem + swap storeb
	// Swap pattern and index for loop increment
	swap
    1 +
  endwhile
  drop drop
  
  // Increment line counter
  1 +
endwhile
drop
